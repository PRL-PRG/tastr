/* -*- C++ -*- */
%option outfile="Lexer.cxx"
%option header-file="Lexer.hxx"
%option yyclass="rtype::parser::Lexer"
%option debug
%option nodefault
%option noyywrap
%option c++

%{
    #include <string>
    #include "Lexer.hpp"
    #include "ParsingContext.h"

    /* Code run each time a pattern is matched. */
    #define YY_USER_ACTION  yylloc->columns(yyleng);

    #define HANDLE_TOKEN(token_name)                    \
        yylval->build<std::string>(yytext);             \
        return rtype::parser::Parser::token::token_name;

%}

INTEGER      integer|int|i
DOUBLE       double|dbl|d
COMPLEX      complex|clx|x
CHARACTER    character|chr|s
LOGICAL      logical|lgl|l
RAW          raw|r
ENVIRONMENT  environment|env|t
EXPRESSION   expression|exr|e
BLANK        [ \t\r]
TYPEDECL     type
PACKAGENAME  [a-zA-Z]+
IDENTIFIER   [a-zA-Z.][a-zA-Z0-9._]*

%%

%{
    /* Code run each time yylex is called. */
    yylloc -> step();
%}

{BLANK}+                    { yylloc -> step(); }
\n+                         { yylloc -> lines(yyleng); yylloc -> step(); }
"|"                         { HANDLE_TOKEN(TOKEN_OR) }
"!"                         { HANDLE_TOKEN(TOKEN_EXCLAMATION) }
"=>"                        { HANDLE_TOKEN(TOKEN_ARROW) }
","                         { HANDLE_TOKEN(TOKEN_COMMA) }
"(("                        { HANDLE_TOKEN(TOKEN_DOUBLE_LPAREN) }
"("                         { HANDLE_TOKEN(TOKEN_LPAREN) }
"))"                        { HANDLE_TOKEN(TOKEN_DOUBLE_RPAREN) }
")"                         { HANDLE_TOKEN(TOKEN_RPAREN) }
"["                         { HANDLE_TOKEN(TOKEN_LBRACKET) }
"]"                         { HANDLE_TOKEN(TOKEN_RBRACKET) }
"<"                         { HANDLE_TOKEN(TOKEN_LANGLEBRACKET) }
">"                         { HANDLE_TOKEN(TOKEN_RANGLEBRACKET) }
"{{"                        { HANDLE_TOKEN(TOKEN_DOUBLE_LBRACE) }
"}}"                        { HANDLE_TOKEN(TOKEN_DOUBLE_RBRACE) }
";"                         { HANDLE_TOKEN(TOKEN_SEMICOLON) }
":"                         { HANDLE_TOKEN(TOKEN_COLON) }
{INTEGER}                   { HANDLE_TOKEN(TOKEN_INTEGER) }
{DOUBLE}                    { HANDLE_TOKEN(TOKEN_DOUBLE) }
{COMPLEX}                   { HANDLE_TOKEN(TOKEN_COMPLEX) }
{CHARACTER}                 { HANDLE_TOKEN(TOKEN_CHARACTER) }
{LOGICAL}                   { HANDLE_TOKEN(TOKEN_LOGICAL) }
{RAW}                       { HANDLE_TOKEN(TOKEN_RAW) }
{ENVIRONMENT}               { HANDLE_TOKEN(TOKEN_ENVIRONMENT) }
{EXPRESSION}                { HANDLE_TOKEN(TOKEN_EXPRESSION) }
{TYPEDECL}                  { HANDLE_TOKEN(TOKEN_TYPEDECL) }
{PACKAGENAME}::{IDENTIFIER} { HANDLE_TOKEN(TOKEN_QUALFUN) }
{IDENTIFIER}                { HANDLE_TOKEN(TOKEN_TAG) }
.                           {
                                std::cout << "Lexer Error: " << *yylloc << " :: " << "invalid character: '" << yytext << "'" << std::endl;
                                throw rtype::parser::Parser::syntax_error(*yylloc, "invalid character: " + std::string(yytext));
                            }
<<EOF>>                     return rtype::parser::Parser::token::TOKEN_EOF;
%%

/* REFERENCE: https://coldfix.eu/2015/05/16/bison-c++11/ */
rtype::parser::Lexer::Lexer(rtype::parser::ParsingContext& context): yyFlexLexer(context.get_input_stream(), std::cout) {
}


